;------------------------------------------------------------
;通信機能
;------------------------------------------------------------
@MAOUNET
#DIM LINE, 1
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
PRINTL [0] 다른 이의 던전에 노예를 보낸다
PRINTL [1] 다른 이의 던전에서 용사를 부른다
PRINTL [2] 다른 이의 던전에서 온 용사들을 전부 떠나도록 한다
PRINTFORML [3] 용사의 레벨 상한 수준을 설정한다 (현재:Lv{FLAG:76})
PRINTFORML [4] 용사를 1레벨로 고정시킨다 (현재:\@FLAG:77?ＯＮ#ＯＦＦ\@)
PRINTL [9] 돌아간다
INPUT
IF RESULT == 0
	CALL EXPORT
ELSEIF RESULT == 1
	CALL INPORT_A
ELSEIF RESULT == 2
	VARSET GLOBALS, "", 0, 100
	SAVEGLOBAL
	PRINTW 등록 용사 정보를 삭제했습니다
ELSEIF RESULT == 3
	PRINTFORML 침입하는 용사 레벨 상한을 입력해 주십시오
	PRINTFORML 또한, 0이하의 값을 입력 하신다면 용사의 출현이 없어집니다
	INPUT
	FLAG:76 = RESULT
ELSEIF RESULT == 4
	INVERTBIT FLAG:77, 0
ELSEIF RESULT == 9
	RETURN 0
ENDIF
GOTO START

;------------------------------------------------------------
;出力キャラの決定関数
;------------------------------------------------------------
@EXPORT
#DIM CHOICE, 5
#DIM LINE, 2
VARSET CHOICE, -1
LOCAL:1 = 0
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
FOR LOCAL, 0, CHARANUM
	;主人は除外
	SIF LOCAL == MASTER
		CONTINUE
	;敵は除外
	SIF CFLAG:LOCAL:1
		CONTINUE
	RESETCOLOR
	;選択済みのキャラは水色
	SIF MATCH(CHOICE, LOCAL)
		SETCOLOR 0x66FFFF
 	PRINTFORM [{LOCAL}] %SAVESTR:LOCAL% %GET_JOB_NAME(LOCAL),8,LEFT% 
	PRINTFORML {CFLAG:LOCAL:9}LV　HP {BASE:LOCAL:0}／{MAXBASE:LOCAL:0} 조교횟수 {CFLAG:LOCAL:10}
NEXT
RESETCOLOR
DRAWLINE
PRINTFORML 낮선 대륙으로 보낼 노예를 선택해 주십시오 ({5-MATCH(CHOICE,-1)}/5)
DRAWLINE
SIF MATCH(CHOICE,-1) == 5
	SETCOLOR 0x404040
PRINTFORML [ 99] 결정
RESETCOLOR
PRINTFORML [100] 취소
$INPUT_LOOP1
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 99
	SIF MATCH(CHOICE,-1) == 5
		GOTO INPUT_LOOP1
	FOR LOCAL, 0, 5
		SIF CHOICE:LOCAL == -1
			CONTINUE
		PRINTFORML %SAVESTR:(CHOICE:LOCAL)%
	NEXT
	PRINTFORML 이사 {5-MATCH(CHOICE,-1)}명으로 괜찮으십니까？
	PRINTL [0] 네
	PRINTL [1] 아니요
	$INPUT_LOOP2
	INPUT
	IF RESULT == 0
		SAVEDATA 999, "대피데이터"
		FOR LOCAL, 0, CHARANUM
			IF MATCH(CHOICE, LOCAL)
				NICKNAME:LOCAL = %SAVESTR:LOCAL%
				CFLAG:LOCAL:190 = GETMILLISECOND()+LOCAL
			ELSE
				NICKNAME:LOCAL = 삭제
			ENDIF
		NEXT
		FOR LOCAL, CHARANUM-1, -1, -1
			SIF NICKNAME:LOCAL == "삭제"
				DELCHARA LOCAL
		NEXT
		PRINTFORML 파티 명을 입력해 주십시오
		LINE:1 = LINECOUNT
		$INPUT_LOOP3
		INPUTS
		PRINTFORML 「%RESULTS%」으로 괜찮으십니까？？
		PRINTL [0] 네
		PRINTL [1] 아니요
		INPUT
		IF RESULT == 0
			LOCALS = %RESULTS% 
			GETTIME
			LOCALS = %RESULTS% %LOCALS%
			SAVEDATA 1000, LOCALS
			PRINTFORMW SAVE1000에 파티를 만들었습니다!
			LOADDATA 999
		ELSEIF RESULT == 1
			GOTO INPUT_LOOP3
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		LOADDATA 1020
	ELSEIF RESULT == 1
		GOTO START
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF
ELSE
	IF RESULT != MASTER && RESULT < CHARANUM && RESULT >= 0 && !CFLAG:RESULT:1
		IF MATCH(CHOICE, RESULT)
			FOR LOCAL, 0, 5
				SIF CHOICE:LOCAL == RESULT
					CHOICE:LOCAL = -1
			NEXT
		ELSE
			IF !MATCH(CHOICE,-1)
				PRINTFORMW 한번에 보낼 수 있는 파티는 5명 까지 입니다
				GOTO START
			ELSE
				FOR LOCAL, 0, 5
					IF CHOICE:LOCAL == -1
						CHOICE:LOCAL = RESULT
						BREAK
					ENDIF
				NEXT
			ENDIF
		ENDIF
		GOTO START
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP1
	ENDIF
ENDIF
GOTO START

@INPORT_A
#DIM SAVE
DRAWLINE
PRINTL 불러올 번호를 선택해 주십시오
DRAWLINE
FOR LOCAL, 0 , 20
	CHKDATA  LOCAL+1000
	PRINTFORML [{LOCAL ,3}] \@ !RESULT ? %RESULTS% # ---- \@
NEXT
PRINTL [99] 취소
$INPUT_LOOP
INPUT
SAVE = RESULT + 1000
IF RESULT == 99
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 19
	CHKDATA SAVE
	IF RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	SAVEDATA 999, "작업"
	LOADDATA SAVE
ELSE
	CLEARLINE 1
	REUSELASTLINE 잘못된 값입니다
	GOTO INPUT_LOOP
ENDIF
GOTO INPUT_LOOP

;キャラデータをGLOBALに出力する
@INPORT_B
#DIM CHARA
#DIM ADD_NUM
FOR LOCAL, 0, 101
	IF LOCAL == 100
		PRINTW 다른 곳에서 용사가 가득 차 더 이상 추가 할 수 없습니다! 
		BREAK
	ENDIF
	SIF CHARA >= CHARANUM
		BREAK
	SIF STRLENS(GLOBALS:LOCAL) > 0
		CONTINUE
	FOR LOCAL:1, 0, 100
		SPLIT GLOBALS:(LOCAL:1), "_", RESULTS
		LOCAL:2 = 0
		IF RESULTS == TOSTR(CFLAG:CHARA:190)
			LOCAL--
			CHARA++
			;PRINTW 同一勇者がいます! 
			LOCAL:2 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:2
		CONTINUE
	GLOBALS:LOCAL += @"{CFLAG:CHARA:190}_"
	GLOBALS:LOCAL += @"{NO:CHARA}_"
	GLOBALS:LOCAL += @"{CFLAG:CHARA:9}_"
	GLOBALS:LOCAL += @"%NICKNAME:CHARA%_"
	FOR LOCAL:1, 0, VARSIZE("ABL")
		SIF ABL:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{ABL:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("BASE")
		SIF BASE:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{BASE:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("BASE")
		SIF MAXBASE:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{MAXBASE:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("CFLAG")
		SIF CFLAG:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{CFLAG:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("EXP")
		SIF EXP:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{EXP:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("EQUIP")
		SIF EQUIP:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{EQUIP:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("PALAM")
		SIF JUEL:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{JUEL:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("TALENT")
		SIF TALENT:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{TALENT:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("MARK")
		SIF MARK:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{MARK:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("CSTR")
		SIF STRLENS(CSTR:CHARA:(LOCAL:1))
			GLOBALS:LOCAL += @"{LOCAL:1},%CSTR:CHARA:(LOCAL:1)%/"
	NEXT
	CHARA++
	ADD_NUM++
NEXT
PRINTFORMW \@ADD_NUM ? {ADD_NUM}명의 용사를 추가했습니다#추가할 수 있는 용사가 없습니다\@
SAVEGLOBAL
LOADDATA 999

