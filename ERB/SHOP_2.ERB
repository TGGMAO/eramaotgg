;-------------------------------------------------
;능력치 업
;-------------------------------------------------
@ABILITY_UP
#DIM SELECT_MENU = 998

$INPUT_LOOP_MENU

PRINTL 누구의 능력치를 올리시겠습니까？
IF SELECT_MENU == 997
	CALL LIFE_LIST_ENEMY
ELSE
	;イレギュラーも含めて正規の値に戻す
	SELECT_MENU = 998
	CALL LIFE_LIST
ENDIF
PRINTFORM [999] - 돌아온다 

;ダンジョンレベル20以上からアンロック
SIF CFLAG:0:9 < 20
	SETCOLOR (GETDEFCOLOR() - 0x444444)
PRINTFORM [998] - 노예 목록  [997] - 용사 목록

RESETCOLOR
PRINTL  

$INPUT_LOOP_0
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF (RESULT > 990 && RESULT < 999) && CFLAG:0:9 < 20
	PRINTW 이 커맨드을 실행하려면、던전의 레벨을 좀 더 높여야합니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
ELSEIF RESULT > 990 && RESULT < 999
	;メニューは一応991～998に含みを持たせています
	;イレギュラーは全て노예一覧になります
	SELECT_MENU = RESULT
	GOTO INPUT_LOOP_MENU
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTW 범위 외의 수치입니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;臨死中のキャラは排除
ELSEIF  BASE:RESULT:0 < 1
	PRINTW 임사(죽을고비)중이기에、선택 할 수 없습니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;自分は排除
ELSEIF RESULT == 0
	PRINTW 자신의 능력은 올려지지 않습니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;노예待機以外は除外
ELSEIF CFLAG:RESULT:1 != 0 && SELECT_MENU == 998
	PRINTW 대기상태가 아니기 때문에、선택할 수 없습니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;侵攻中勇者以外は除外
ELSEIF CFLAG:RESULT:1 != 2 && SELECT_MENU == 997
	PRINTW 침공중인 상태이기 때문에、선택할 수 없습니다
	CLEARLINE 2
	GOTO INPUT_LOOP_0
ENDIF

T = TARGET
TARGET = RESULT

$INPUT_LOOP_1
PRINTFORML %SAVESTR:TARGET%
PRINTL ‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥‥
CALL SHOW_INFO_EXP
CALL SHOW_JUEL
CALL SHOW_ABLUP_SELECT

INPUT

;Ｃ감각
IF RESULT == 0
	CALL ABLUP0
;Ｂ감각
ELSEIF RESULT == 1
	CALL ABLUP1
;Ｖ감각
ELSEIF RESULT == 2
	CALL ABLUP2
;Ａ감각
ELSEIF RESULT == 3
	CALL ABLUP3
;Ｆ감각
ELSEIF RESULT == 4
	CALL ABLUP4
;순종
ELSEIF RESULT == 10
	CALL ABLUP10
;욕망
ELSEIF RESULT == 11
	CALL ABLUP11
;기교
ELSEIF RESULT == 12
	CALL ABLUP12
;봉사기술
ELSEIF RESULT == 13
	CALL ABLUP13
;성교기술
ELSEIF RESULT == 14
	CALL ABLUP14
;화술
ELSEIF RESULT == 15
	CALL ABLUP15
;봉사정신
ELSEIF RESULT == 16
	CALL ABLUP16
;노출벽
ELSEIF RESULT == 17
	CALL ABLUP17
;새드끼
ELSEIF RESULT == 20
	CALL ABLUP20
;마조끼
ELSEIF RESULT == 21
	CALL ABLUP21
;레즈끼
ELSEIF RESULT == 22
	CALL ABLUP22
;ホモっ気
ELSEIF RESULT == 23
	CALL ABLUP23
;성교중독
ELSEIF RESULT == 30
	CALL ABLUP30
;자위중독
ELSEIF RESULT == 31
	CALL ABLUP31
;정액중독
ELSEIF RESULT == 32
	CALL ABLUP32
;레즈중독
ELSEIF RESULT == 33
	CALL ABLUP33
;매춘중독
ELSEIF RESULT == 37
	CALL ABLUP37
;수간중독
ELSEIF RESULT == 39
	CALL ABLUP39
;Ｆ중독
ELSEIF RESULT == 40
	CALL ABLUP40
;반발각인
ELSEIF RESULT == 99
	CALL ABLUP99
ELSEIF RESULT == 999
	CALL YOKUBO_UP_CHECK
	CALL CHECK_SELLASSIABLE
	CALL CHECK_SPECIALSKIL
	TARGET = T
	RESTART
ENDIF

GOTO INPUT_LOOP_1

;-----------------------------------
@INTERCEPT
#DIM SELECT
#DIM ROOMID
#DIM ROOM
#DIM FLOOR
#DIM WORK
#DIM ITEM_GET
#DIM COST
;------------------------------------
;迎撃指定
;SELECT = 指定された迎撃노예
;ROOMID = 施設の場所
;ROOM = 시설
;FLOOR = 出発階層
;WORK  = 迎撃時の行動
;ITEM_GET  = アイテム補給

$INPUT_LOOP_2

PRINTL 누가 용자를 요격 해 줍니까？
COST = 6000
PRINTFORML [売却可]이 아닌 경우, 자금이 {COST}pt 필요합니다.
CALL LIFE_LIST
PRINTFORML [999] - 돌아온다

INPUT
IF RESULT == 999
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP_2
;臨死中のキャラは排除
ELSEIF  BASE:RESULT:0 < 1
	GOTO INPUT_LOOP_2
;自分は排除
ELSEIF RESULT == 0
	PRINTL 자신은 요격 할 수 없습니다
	WAIT
	GOTO INPUT_LOOP_2
;노예待機以外は除外
ELSEIF CFLAG:RESULT:1 != 0
	GOTO INPUT_LOOP_2
;売却可以外は除外
ELSEIF CFLAG:RESULT:0 == 0 && TALENT:RESULT:254 == 0
	PRINTFORMW %조사선택(SAVESTR:RESULT, "는")% 명령을 따를 만큼 온순하지 않다
	GOTO INPUT_LOOP_2
;お金が必要
ELSEIF CFLAG:RESULT:0 == 0 && TALENT:RESULT:254 == 1 && MONEY < COST
	PRINTFORMW 돈이 부족해서 %조사선택(SAVESTR:RESULT, "는")% 말을 듣지 않는다
	GOTO INPUT_LOOP_2
;妊婦は除外
ELSEIF TALENT:RESULT:153 == 1 && GETBIT(FLAG:5,10) == 0
	PRINTFORMW %SAVESTR:RESULT%는 임신을 했다, 출격은 허용되지 않는다
	GOTO INPUT_LOOP_2
ENDIF

SIF CFLAG:RESULT:0 == 0
	PRINT 돈을 지불했다
PRINTFORMW *%조사선택(SAVESTR:RESULT, "가")% 용사를 요격 해 줍니다*

SELECT = RESULT

FLOOR = 9
WORK = CFLAG:SELECT:500
ITEM_GET = 0

$INPUT_LOOP_MAIN
PRINTFORML %SAVESTR:SELECT%의 요격 설정
DRAWLINE
PRINTFORML [0] 출발 계층     - {FLOOR}층
PRINTFORM [1] 요격시의 행동  - 
IF WORK == 1
	PRINTL 매춘
ELSEIF WORK == 2
	PRINTL 함정보충
ELSEIF WORK == 3
	PRINTL 시설확장(2000G필요)
ELSEIF WORK == 4
	PRINTL 잡입공작
ELSEIF WORK == 5
	PRINTL 훈련
ELSE
	PRINTL 부업
ENDIF
PRINTFORM [2] 아이템의 보급 - 
IF ITEM_GET == 1
	PRINTL 유(2000G필요)
ELSE
	PRINTL 무
ENDIF
PRINTL  
SETCOLORBYNAME DarkSeaGreen
PRINTFORM [998] 요격개시！　　
RESETCOLOR
PRINTFORML [999] 돌아 가기

INPUT

IF RESULT == 999
	GOTO INPUT_LOOP_2
ELSEIF RESULT == 0
	GOTO INPUT_LOOP_4
ELSEIF RESULT == 1
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 2 && ITEM_GET == 0
	IF MONEY < 2000 
		PRINTFORMW * 자금이 부족합니다 *
		GOTO INPUT_LOOP_MAIN
	ELSEIF CFLAG:SELECT:0 == 0 && MONEY < (COST + 2000)
		PRINTFORMW * 자금이 부족합니다 *
		GOTO INPUT_LOOP_MAIN
	ENDIF
	ITEM_GET = 1
	GOTO INPUT_LOOP_MAIN
ELSEIF RESULT == 2
	ITEM_GET = 0
	GOTO INPUT_LOOP_MAIN
ELSEIF RESULT != 998
	GOTO INPUT_LOOP_MAIN
ENDIF

;出撃決定
CFLAG:SELECT:1 = 3
CFLAG:SELECT:500 = WORK
CFLAG:SELECT:501 = FLOOR
CFLAG:SELECT:502 = 90
CFLAG:SELECT:505 = 0
SIF CFLAG:SELECT:0 == 0
	MONEY -= COST

SIF WORK == 3
	MONEY -= 2000
IF ITEM_GET == 1
	MONEY -= 2000
	;入手アイテム数
	LOCAL:2 = 0
	FOR LOCAL:1, 0, 3
		CALL ADD_EX_ITEM, -1, SELECT , 2
		SIF RESULT > 0
			LOCAL:2 += 1
	NEXT
	;返金
	IF LOCAL:2 == 0
		PRINTFORML 아이템은 필요 없다며、군자금을 반환했다
		MONEY += 2000
	ENDIF
ENDIF

CALL GOHOUBI_REQUEST, SELECT

RETURN 0

;------------------------
;出発階層設定
;------------------------
$INPUT_LOOP_4
PRINTL 출발층 설정
PRINTFORML 마왕의 힘으로 %SAVESTR:SELECT%를 좋아하는 층으로 전송할 수 있습니다。
PRINTL 몇층에 내보냅니까？ (1-9)
PRINTL [1] [2] [3] [4] [5] [6] [7] [8] [9]

INPUT
IF RESULT >= 1 && RESULT <= 9
	FLOOR = RESULT
ELSE
	GOTO INPUT_LOOP_4
ENDIF

IF WORK == 3
	ROOMID = FLOOR + 349
	
	ROOM = FLAG:ROOMID
	
	IF ROOM == 0
		PRINTFORMW {FLOOR}층에는 시설이 없습니다
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	ROOMID += 10
	LOCAL = FLAG:ROOMID
	
	IF LOCAL == 3
		PRINTFORMW {FLOOR}층의 %ITEMNAME:ROOM%는 벌써 한계까지 확장하였습니다
		GOTO INPUT_LOOP_MAIN
	ENDIF
ENDIF

GOTO INPUT_LOOP_MAIN

;------------------------
;行動設定
;------------------------
$INPUT_LOOP_3

PRINTL 던전내에서의 행동
DRAWLINE
PRINTL [0] 부업 
IF CFLAG:0:9 >= 10
	PRINTL [1] 매춘 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 미개방 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 20
	PRINTL [2] 함정의 보충 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 미개방 
	RESETCOLOR
ENDIF


IF CFLAG:0:9 >= 30
	PRINTL [3] 시설 확장(2000G 필요) 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 미개방 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 40
	PRINTL [4] 잠입공작 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 미개방 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 50
	PRINTL [5] 훈련
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 미개방 
	RESETCOLOR
ENDIF

DRAWLINE


INPUT
IF RESULT < 0
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 1 && CFLAG:0:9 < 10
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 2 && CFLAG:0:9 < 20
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 3 && CFLAG:0:9 < 30
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 4 && CFLAG:0:9 < 40
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 5 && CFLAG:0:9 < 50
	GOTO INPUT_LOOP_3
ELSEIF RESULT >= 6
	GOTO INPUT_LOOP_3
ENDIF

IF RESULT == 1
	PRINTW 던전의 몬스터 상대에게 매춘을 허가합니다
ELSEIF RESULT == 2
	PRINTW 함정의 보충 작업을 실시합니다
ELSEIF RESULT == 3
	ROOMID = FLOOR + 349
	
	ROOM = FLAG:ROOMID
	
	IF ROOM == 0
		PRINTFORMW {FLOOR}층에는 시설이 없습니다
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	ROOMID += 10
	LOCAL = FLAG:ROOMID
	
	IF LOCAL == 3
		PRINTFORMW {FLOOR}층의 %ITEMNAME:ROOM%은 벌써 한계까지 확장하였습니다
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	PRINTFORMW {FLOOR}층의 %ITEMNAME:ROOM%를 확장합니다。자금이 2000 필요합니다
	
	IF MONEY < 2000 
		PRINTFORMW * 자금이 부족합니다 *
		GOTO INPUT_LOOP_MAIN
	ENDIF

ELSEIF RESULT == 4
	PRINTW 용자의 파티에 잠입해 공작 활동을 실시합니다
ELSEIF RESULT == 5
	PRINTW 요격 중에 훈련을 실시하여 경험치를 얻습니다.
ELSE
	PRINTW 수입을 얻습니다
ENDIF
WORK = RESULT

GOTO INPUT_LOOP_MAIN

RETURN 0

;------------------------
@GOHOUBI_REQUEST, ARG:0
#DIM SELECT
#DIM WISH
;------------------------
;SELECT = 選択された노예
;WISH = 希望するもの

SELECT = ARG:0

IF TALENT:SELECT:136 == 1 && RAND:3 == 0
	WISH = RAND:3 + 1
ELSEIF TALENT:SELECT:85 == 1
	WISH = RAND:3 + 4
	IF WISH == 6 && !TALENT:MASTER:121 && !TALENT:MASTER:122
		WISH = 4
	ENDIF
ELSEIF TALENT:SELECT:76 == 1
	WISH = RAND:3 + 7
ELSE
	WISH = 0
ENDIF


CFLAG:SELECT:504 = WISH
A = SELECT

CALL GOHOUBI_REQUEST_KOUJO

A = 0



